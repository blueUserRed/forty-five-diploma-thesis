
\chapter{Anhang 2 - Onj Grammatik}\label{ch:anhang-2}

\renewcommand{\kapitelautor}{Autor: Marvin Kurka}

Unten angegeben ist die vollständige Grammatik für .onj Dateien.
Der verwendete Syntax ist eine Form der Backus-Naur-Form:

\begin{itemize}
    \item \inlineCode{?} bedeutet optional
    \item \inlineCode{*} bedeutet null oder mehr
    \item \inlineCode{+} bedeutet eins oder mehr
    \item \inlineCode{|} bedeutet oder
    \item \inlineCode{(...)} gruppiert eine expression
\end{itemize}

%! language = bnf
\begin{minted}{bnf}
    
OnjFile ::= ((objectEntry COMMA) | topLevelStructure)* objectEntry? topLevelStructure?

topLevelStructure ::= importStructure | variableStructure | useStructure

importStructure ::= IMPORT (STRING | literalExpression) asContextDependentKeyword variableDeclarationName SEMICOLON
variableDeclarationName ::= IDENTIFIER
variableStructure ::= VAR variableDeclarationName EQUALS expression SEMICOLON
useStructure ::= USE IDENTIFIER SEMICOLON

asContextDependentKeyword ::= "as"

keyValuePair ::= key COLON expression
key ::= (IDENTIFIER | STRING)
tripleDot ::= DOT DOT DOT literalExpression

objectEntry ::= tripleDot | keyValuePair
arrayEntry ::= tripleDot | expression

expression ::= infixFunctionCallExpression
infixFunctionCallExpression ::= binaryExpression (functionName infixFunctionCallExpression)?
binaryExpression ::= typeConversionExpression (binaryOperator binaryExpression)?
typeConversionExpression ::= unaryExpression (HASH functionName)*
unaryExpression ::= (MINUS unaryExpression) | variableAccessExpression
variableAccessExpression ::= literalExpression (DOT (variableAccessor | literalExpression))*
literalExpression ::=   functionCallExpression |
                        INTEGER |
                        FLOAT |
                        groupedExpression |
                        object |
                        namedObject |
                        array |
                        variableExpression |
                        STRING

variableExpression ::= IDENTIFIER
variableAccessor ::= IDENTIFIER | STRING
functionCallExpression ::= functionName L_PAREN (expression COMMA)* expression? R_PAREN
functionName ::= IDENTIFIER
groupedExpression ::= L_PAREN expression R_PAREN

object ::= L_BRACE (objectEntry COMMA)* objectEntry? R_BRACE
namedObject ::= DOLLAR namedObjectName object
namedObjectName ::= IDENTIFIER
array ::= L_BRACKET (arrayEntry COMMA)* arrayEntry? R_BRACKET

binaryOperator ::= PLUS | MINUS | STAR | DIV

\end{minted}

\begin{infoBox}
    "as" ist im Parser als ein context dependent keyword, auch soft keyword genannt, implementiert.
    Das heißt, das es nur im Kontext eines import-statements als keyword erkannt wird, anders als \zB var.
    Dadurch ist es möglich eine variable oder einen key "as" zu nennen.
\end{infoBox}

% resets author
\renewcommand{\kapitelautor}{}
